# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles

#
# CONFIG YOUR .localrc !!!
#

# Split the .localrc fine into a genric and a project/environment specific part
# and change contents dependingon the context.
if [[ -a ~/.localrc.default ]]
then
  source ~/.localrc.default
fi

# Stash your project specific environment variables in ~/.localrc. 
# This means they'll stay out of your main dotfiles repository (which may be public, like this one), 
# but you'll have access to them in your scripts.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

# Better history
# Credits to https://coderwall.com/p/jpj_6q/zsh-better-history-searching-with-arrow-keys
autoload -U up-line-or-beginning-search
autoload -U down-line-or-beginning-search
zle -N up-line-or-beginning-search
zle -N down-line-or-beginning-search
bindkey "^[[A" up-line-or-beginning-search # Up
bindkey "^[[B" down-line-or-beginning-search # Down

# Add JBang to environment
alias j!=jbang
export PATH="$HOME/.jbang/bin:$PATH"
