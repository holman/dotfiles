# shortcut to this dotfiles path is $DOTS
# if not using ALT_HOME use normal HOME
if [ -z "$ALT_HOME" ]; then
	export DOTS=$HOME/.dotfiles
else
	export DOTS=$ALT_HOME/.dotfiles
fi

# Enable colors
if [[ "$TERM" != *-256color ]]; then
  export TERM=xterm-256color
fi

DISABLE_UPDATE_PROMPT="true"

# Periodically (typically once a day), update from github
source $DOTS/script/check_for_upgrade.sh

# First, load oh-my-zsh
source $DOTS/zsh/oh-my.zsh.preamble

# your project folder that we can `c [tab]` to
export PROJECTS=~/Documents/repos/

# all of our zsh files
typeset -U config_files
config_files=($DOTS/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files
